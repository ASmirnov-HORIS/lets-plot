plugins {
    id 'kotlin-multiplatform' version '1.3.30'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '0.5'
}
repositories {
    mavenCentral()
}

kotlin {
    jvm()
    jvm("jvmBatik")
    jvm("jvmJfx")
    js()
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

                implementation project(':base')
                implementation project(':mapper-core')
                implementation project(':visualization-base-svg')
                implementation project(':visualization-plot-base')
                implementation project(':visualization-plot-common')
                implementation project(':visualization-plot-builder')
                implementation project(':visualization-plot-config')
            }
        }

        allJvm {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                compileOnly "io.github.microutils:kotlin-logging:$kotlinLogging_version"
                implementation "org.apache.commons:commons-math3:$math3_version"
            }
        }
        jvmMain {
            dependsOn allJvm
        }
        jvmBatikMain {
            dependsOn allJvm
            dependsOn jvmMain
            dependencies {
                implementation project(':visualization-base-svg-mapper')   // Batik-based mapper

                implementation "org.apache.xmlgraphics:batik-anim:$batik_version"
                implementation "org.apache.xmlgraphics:batik-awt-util:$batik_version"
                implementation "org.apache.xmlgraphics:batik-bridge:$batik_version"
                implementation "org.apache.xmlgraphics:batik-codec:$batik_version"
                implementation "org.apache.xmlgraphics:batik-css:$batik_version"
                implementation "org.apache.xmlgraphics:batik-dom:$batik_version"
                implementation "org.apache.xmlgraphics:batik-ext:$batik_version"
                implementation "org.apache.xmlgraphics:batik-gvt:$batik_version"
                implementation "org.apache.xmlgraphics:batik-parser:$batik_version"
                implementation "org.apache.xmlgraphics:batik-script:$batik_version"
                implementation "org.apache.xmlgraphics:batik-svg-dom:$batik_version"
                implementation "org.apache.xmlgraphics:batik-swing:$batik_version"
                implementation "org.apache.xmlgraphics:batik-util:$batik_version"
                implementation "org.apache.xmlgraphics:batik-xml:$batik_version"
            }
        }
        jvmJfxMain {
            dependsOn allJvm
            dependsOn jvmMain

            dependencies {
                implementation project(':visualization-base-canvas')
                implementation project(':visualization-base-svg-mapper-jfx')
            }
        }

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
            }
        }
    }
}

idea {
    module {
        settings {
//            packagePrefix["src/commonMain/kotlin"] = "jetbrains.datalore"
//            packagePrefix["src/commonTest/kotlin"] = "jetbrains.datalore"
//            packagePrefix["src/jsMain/kotlin"] = "jetbrains.datalore"
//            packagePrefix["src/jsTest/kotlin"] = "jetbrains.datalore"

            packagePrefix["src/jvmMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmBatikMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmJfxMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmTest/kotlin"] = "jetbrains.datalore"
        }
    }
}