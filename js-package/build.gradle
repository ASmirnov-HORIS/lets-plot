plugins {
    id 'kotlin-multiplatform'
    id "com.moowork.node"
    // publishing to bintray
    id "com.jfrog.bintray"
}
repositories {
    mavenCentral()
}

kotlin {
    js() {
        compileKotlinJs {
            kotlinOptions.sourceMap = true
            kotlinOptions.sourceMapEmbedSources = "always"
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

                implementation project(':base-portable')
                implementation project(':base')
                implementation project(':gis') // LIVEMAP_SWITCH
                implementation project(':plot-livemap') // LIVEMAP_SWITCH
                implementation project(':mapper-core')
                implementation project(':vis-svg')
                implementation project(':vis-svg-mapper')
                implementation project(':plot-base-portable')
                implementation project(':plot-base')
                implementation project(':plot-common-portable')
                implementation project(':plot-common')
                implementation project(':plot-builder-portable')
                implementation project(':plot-builder')
                implementation project(':plot-config-portable')
                implementation project(':plot-config')
                implementation project(':plot-app')
            }
        }

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
            }
        }
    }
}

node {
    version = '10.16.3'
    npmVersion = '6.9.0'
    download = true
}

def BINTRAY_USER = project.buildSettings.bintray.user
def BINTRAY_KEY = project.buildSettings.bintray.key

if (BINTRAY_USER != null && BINTRAY_KEY != null) {
    bintray {
        user = BINTRAY_USER
        key = BINTRAY_KEY

        filesSpec {
            from 'build/dist'
            into '.'
        }

        publish = true

        pkg {
            repo = 'datalore-plot'
            name = 'datalore-plot-js'
            licenses = ['MIT']
            vcsUrl = 'https://github.com/JetBrains/datalore-plot'
            version {
                name = project.buildSettings.version
            }
        }
    }
}

repositories.whenObjectAdded {
    if (it instanceof IvyArtifactRepository) {
        metadataSources {
            artifact()
        }
    }
}

// ---------------------------------
// Trick to copy kotlin.js etc. to `output`
configurations {
    libraryDependenciesJs
    projectDependenciesJs
}

dependencies {
    libraryDependenciesJs "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    libraryDependenciesJs "io.github.microutils:kotlin-logging-js:$kotlinLogging_version"
    libraryDependenciesJs "io.ktor:ktor-client-websockets:$ktor_version"
    libraryDependenciesJs "io.ktor:ktor-client-core-js:$ktor_version"
    libraryDependenciesJs "io.ktor:ktor-client-js:$ktor_version"

    projectDependenciesJs project(':base-portable')
    projectDependenciesJs project(':base')
    projectDependenciesJs project(':gis') // LIVEMAP_SWITCH
    projectDependenciesJs project(':livemap') // LIVEMAP_SWITCH
    projectDependenciesJs project(':plot-livemap') // LIVEMAP_SWITCH
    projectDependenciesJs project(':mapper-core')
    projectDependenciesJs project(':vis-svg')
    projectDependenciesJs project(':vis-svg-mapper')

    projectDependenciesJs project(':vis-canvas')         // required by plot-builder (get rid?)

    projectDependenciesJs project(':plot-common-portable')
    projectDependenciesJs project(':plot-common')
    projectDependenciesJs project(':plot-base-portable')
    projectDependenciesJs project(':plot-base')
    projectDependenciesJs project(':plot-builder-portable')
    projectDependenciesJs project(':plot-builder')
    projectDependenciesJs project(':plot-config-portable')
    projectDependenciesJs project(':plot-config')
    projectDependenciesJs project(':plot-app')
}

// ---------------------------------
def OUTPUT_DIR_JS_LIB = "${project.buildDir}/js"

def task_group = 'datalore plot'

task copyLibraryDependenciesJs(type: Copy) {
    configurations.libraryDependenciesJs.each {
        println "library dependency: ${it.absolutePath}"
        from zipTree(it.absolutePath).matching { include '*.js' }
    }
    into OUTPUT_DIR_JS_LIB
}

task copyProjectDependenciesJs(type: Copy) {
    configurations.projectDependenciesJs.each {
        def jarJs = it.absolutePath.replace("-metadata-", "-js-")
        println "project dependency: $jarJs"
        from zipTree(jarJs).matching { include '*.js' }
    }
    into OUTPUT_DIR_JS_LIB
}

task webpackBuildProdVersion(type: NpmTask) {
    group = task_group
    args = ['run', 'webpack-build-prod', '--', "--output-filename=datalore-plot-${project.buildSettings.version}.min.js"]
}

task webpackBuildProdLatest(type: NpmTask) {
    group = task_group
    args = ['run', 'webpack-build-prod', '--', "--output-filename=datalore-plot-latest.min.js"]
}

task webpackBuildDevVersion(type: NpmTask) {
    group = task_group
    args = ['run', 'webpack-build-dev', '--', "--output-filename=datalore-plot-${project.buildSettings.version}.js"]
}

task webpackBuildDevLatest(type: NpmTask) {
    group = task_group
    args = ['run', 'webpack-build-dev', '--', "--output-filename=datalore-plot-latest.js"]
}

task webpackBuildDev {
    group = task_group
    dependsOn webpackBuildDevVersion
    dependsOn webpackBuildDevLatest
}

task webpackBuildProd {
    group = task_group
    dependsOn webpackBuildProdVersion
    dependsOn webpackBuildProdLatest
}

webpackBuildProd.dependsOn += copyLibraryDependenciesJs
webpackBuildProd.dependsOn += copyProjectDependenciesJs
webpackBuildProd.dependsOn += npmInstall
webpackBuildDev.dependsOn += copyLibraryDependenciesJs
webpackBuildDev.dependsOn += copyProjectDependenciesJs
webpackBuildDev.dependsOn += npmInstall

assemble.dependsOn += webpackBuildDev
assemble.dependsOn += webpackBuildProd
