plugins {
    id 'kotlin-multiplatform'
    id "com.moowork.node"
}

kotlin {
    js() {
        compileKotlinJs {
            kotlinOptions.sourceMap = true
            kotlinOptions.sourceMapEmbedSources = "always"
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

                implementation project(':base-portable')
                implementation project(':base')
                implementation project(':mapper-core')
                implementation project(':vis-svg-portable')
                implementation project(':vis-canvas')
                implementation project(':vis-svg-mapper')
                implementation project(':plot-base-portable')
                implementation project(':plot-base')
                implementation project(':plot-common-portable')
                implementation project(':plot-common')
                implementation project(':plot-builder-portable')
                implementation project(':plot-builder')
                implementation project(':plot-config-portable')
                implementation project(':plot-config')

                if (project.buildSettings.enable_livemap) {
                    implementation project(':gis')
                    implementation project(':plot-livemap')
                } else {
                    implementation project(':plot-livemap-stub')
                }
            }
        }

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "io.github.microutils:kotlin-logging-js:$kotlinLogging_version"

                implementation project(':mapper-core')
                implementation project(':vis-svg-mapper')
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}

node {
    version = '10.16.3'
    npmVersion = '6.9.0'
    download = true
}


def artifact_version = project.js_artifact_version


repositories.whenObjectAdded {
    if (it instanceof IvyArtifactRepository) {
        metadataSources {
            artifact()
        }
    }
}

/*
 * Copyright (c) 2019. JetBrains s.r.o.
 * Use of this source code is governed by the MIT license that can be found in the LICENSE file.
 */

// ---------------------------------
// Trick to copy kotlin.js etc. to `output`
configurations {
    libraryDependenciesJs
    projectDependenciesJs {
        canBeConsumed = false
        canBeResolved = true
    }
}

dependencies {
    libraryDependenciesJs "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    libraryDependenciesJs "io.github.microutils:kotlin-logging-js:$kotlinLogging_version"
    libraryDependenciesJs "io.ktor:ktor-client-websockets:$ktor_version"
    libraryDependenciesJs "io.ktor:ktor-client-core-js:$ktor_version"
    libraryDependenciesJs "io.ktor:ktor-client-js:$ktor_version"

    projectDependenciesJs project(path: ':base-portable', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':base', configuration: 'outputJsJar')

    projectDependenciesJs project(path: ':mapper-core', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':vis-svg-portable', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':vis-svg-mapper', configuration: 'outputJsJar')

    if (project.buildSettings.enable_livemap) {
        projectDependenciesJs project(path: ':gis', configuration: 'outputJsJar')
        projectDependenciesJs project(path: ':livemap', configuration: 'outputJsJar')
        projectDependenciesJs project(path: ':plot-livemap', configuration: 'outputJsJar')
    } else {
        projectDependenciesJs project(path: ':plot-livemap-stub', configuration: 'outputJsJar')
    }

    projectDependenciesJs project(path: ':vis-canvas', configuration: 'outputJsJar')
    // required by plot-builder (get rid?)

    projectDependenciesJs project(path: ':plot-common-portable', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':plot-common', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':plot-base-portable', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':plot-base', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':plot-builder-portable', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':plot-builder', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':plot-config-portable', configuration: 'outputJsJar')
    projectDependenciesJs project(path: ':plot-config', configuration: 'outputJsJar')
}

// ---------------------------------
def js_package_libs_dir = "${project.buildDir}/js"

def task_group = 'datalore plot'

task copyLibraryDependenciesJs(type: Copy) {
    configurations.libraryDependenciesJs.each {
        println "library dependency: ${it.absolutePath}"
        from zipTree(it.absolutePath).matching { include '*.js' }
    }
    into js_package_libs_dir
}

task copyProjectDependenciesJs(type: Copy) {
    configurations.projectDependenciesJs.each {
        def jarJs = it.absolutePath
        println "project dependency: $jarJs"
        from zipTree(jarJs).matching { include '*.js' }
    }
    into js_package_libs_dir
}

task copyJsPackage(type: Copy) {
    from "${project.buildDir}/classes/kotlin/js/main/js-package.js"
    into js_package_libs_dir
}

def output_dir = "${project.buildDir}/dist/"
def input_dir = "${project.buildDir}/js/"

task webpackBuildReleaseVersion(type: NpmTask) {
    inputs.dir(input_dir)
    inputs.property("artifact_version", artifact_version)
    outputs.files("${output_dir}lets-plot-${artifact_version}.min.js")
    args = ['run', 'webpack-build-release', '--', "--output-filename=lets-plot-${artifact_version}.min.js"]
}

task webpackBuildReleaseLatest(type: NpmTask) {
    inputs.dir(input_dir)
    inputs.property("artifact_version", artifact_version)
    outputs.files("${output_dir}lets-plot-latest.min.js")
    args = ['run', 'webpack-build-release', '--', "--output-filename=lets-plot-latest.min.js"]
}

task webpackBuildDebugVersion(type: NpmTask) {
    inputs.dir(input_dir)
    inputs.property("artifact_version", artifact_version)
    outputs.files("${output_dir}lets-plot-${artifact_version}.js")
    args = ['run', 'webpack-build-debug', '--', "--output-filename=lets-plot-${artifact_version}.js"]
}

task webpackBuildDebugLatest(type: NpmTask) {
    inputs.dir(input_dir)
    inputs.property("artifact_version", artifact_version)
    outputs.files("${output_dir}lets-plot-latest.js")
    args = ['run', 'webpack-build-debug', '--', "--output-filename=lets-plot-latest.js"]
}

task webpackBuildDebug {
    group = task_group
    dependsOn webpackBuildDebugVersion
    dependsOn webpackBuildDebugLatest
}

task webpackBuildRelease {
    group = task_group
    dependsOn webpackBuildReleaseVersion
    dependsOn webpackBuildReleaseLatest
}

webpackBuildRelease.dependsOn += copyLibraryDependenciesJs
webpackBuildRelease.dependsOn += copyProjectDependenciesJs
webpackBuildRelease.dependsOn += copyJsPackage
webpackBuildRelease.dependsOn += npmInstall
webpackBuildDebug.dependsOn += copyLibraryDependenciesJs
webpackBuildDebug.dependsOn += copyProjectDependenciesJs
webpackBuildDebug.dependsOn += copyJsPackage
webpackBuildDebug.dependsOn += npmInstall

build.dependsOn += webpackBuildDebug
build.dependsOn += webpackBuildRelease
