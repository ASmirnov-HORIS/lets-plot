plugins {
    id 'kotlin-multiplatform' version '1.3.30'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '0.5'
}
repositories {
    mavenCentral()
}

kotlin {
    jvm("jvmBatik")
    jvm("jvmJfx")
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

                implementation project(':base')
                implementation project(':mapper-core')
                implementation project(':visualization-base-svg')
                implementation project(':visualization-base-svg-mapper')
            }
        }

        allJvm {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                compileOnly "io.github.microutils:kotlin-logging:$kotlinLogging_version"
                implementation "org.apache.commons:commons-math3:$math3_version"
            }
        }
        jvmMain {
            dependsOn allJvm
            dependencies {
            }
        }
        jvmBatikMain {
            dependsOn allJvm
            dependsOn jvmMain
            dependencies {
                implementation project(":visualization-base-svg-mapper-batik")
            }
        }
        jvmJfxMain {
            dependsOn allJvm
            dependsOn jvmMain
            dependencies {
                implementation project(":visualization-base-svg-mapper-jfx")
            }
        }
    }
}

idea {
    module {
        settings {
            packagePrefix["src/jvmJfxMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmBatikMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmMain/kotlin"] = "jetbrains.datalore"
        }
    }
}


// Workaround for Idea/Gradle bug: https://youtrack.jetbrains.com/issue/KT-24463
// MPP: Run does not add resource directory to classpath [Cannot get resource when using common module]
task copyProcessedResources(type: Copy) {
    from "${project.buildDir}/processedResources"
    into "${project.buildDir}/classes/kotlin"
}

build.dependsOn += copyProcessedResources