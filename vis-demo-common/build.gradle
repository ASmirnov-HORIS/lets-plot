plugins {
    id 'kotlin-multiplatform'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'org.jetbrains.gradle.plugin.idea-ext'
}
repositories {
    mavenCentral()
}

kotlin {
    jvm()
    js()
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation project(':base-portable')
                implementation project(':base')
                implementation project(':vis-svg')

                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$kotlinSerialization_version"
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-html-jvm:${kotlinx_html_version}"

                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlinSerialization_version"
                api "com.charleskorn.kaml:kaml:$kaml_version"
                api "org.snakeyaml:snakeyaml-engine:1.0"
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
            }
        }
    }
}

idea {
    module {
        settings {
            packagePrefix["src/jvmMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmTest/kotlin"] = "jetbrains.datalore"
        }
    }
}


// Workaround for Idea/Gradle bug: https://youtrack.jetbrains.com/issue/KT-24463
// MPP: Run does not add resource directory to classpath [Cannot get resource when using common module]
// 
// JavaFX Scene mapping requires stylesheet resource URI 
task copyProcessedResources(type: Copy) {
    from "${project.buildDir}/processedResources"
    into "${project.buildDir}/classes/kotlin"
}

build.dependsOn += copyProcessedResources


