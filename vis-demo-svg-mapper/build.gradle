plugins {
    id 'kotlin-multiplatform'
    id 'org.jetbrains.gradle.plugin.idea-ext'
}
repositories {
    mavenCentral()
}

kotlin {
    jvm("jvmBatik")
    jvm("jvmJfx")
    jvm("jvmBrowser")   // generates index.html and opens it in browser
    js()

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

                implementation project(':base-portable')
                implementation project(':base')
                implementation project(':mapper-core')
                implementation project(':vis-svg')
                implementation project(':vis-svg-mapper')
                implementation project(':vis-demo-common')
                implementation project(':vis-demo-common-jfx')
            }
        }

        allJvm {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                compileOnly "io.github.microutils:kotlin-logging:$kotlinLogging_version"
            }
        }
        jvmBatikMain {
            dependsOn allJvm
            dependencies {
                implementation "org.apache.commons:commons-math3:$math3_version"
                implementation project(":vis-svg-mapper-batik")
            }
        }
        jvmJfxMain {
            dependsOn allJvm
            dependencies {
                implementation "org.apache.commons:commons-math3:$math3_version"
//                runtimeOnly project(':vis-canvas') // needed for `svg transform` parsing
                implementation project(':vis-canvas') // needed for `svg transform` parsing
                implementation project(":vis-svg-mapper-jfx")
            }
        }
        jvmBrowserMain {
            dependsOn allJvm
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-html-jvm:${kotlinx_html_version}"
            }
        }

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
            }
        }
    }
}

idea {
    module {
        settings {
            packagePrefix["src/jvmMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmJfxMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmBatikMain/kotlin"] = "jetbrains.datalore"
            packagePrefix["src/jvmBrowserMain/kotlin"] = "jetbrains.datalore"
        }
    }
}


// ---------------------------------
// Trick to copy kotlin.js etc. to `output`
configurations {
    libraryDependenciesJs
    projectDependenciesJs
}

dependencies {
    libraryDependenciesJs "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    libraryDependenciesJs "io.github.microutils:kotlin-logging-js:$kotlinLogging_version"
    // for Assertions.kt ToDo: get rid of kotlin-test here
    libraryDependenciesJs "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
    projectDependenciesJs project(':base')
    projectDependenciesJs project(':mapper-core')
    projectDependenciesJs project(':vis-svg')
    projectDependenciesJs project(':vis-svg-mapper')
    projectDependenciesJs project(':vis-demo-common')
}

// ---------------------------------
def OUTPUT_DIR_JS = "${project.buildDir}/demoWeb"
def OUTPUT_DIR_JS_LIB = "${OUTPUT_DIR_JS}/lib"

task copyLibraryDependenciesJs(type: Copy) {
    configurations.libraryDependenciesJs.each {
        println "library dependency: ${it.absolutePath}"
        from zipTree(it.absolutePath).matching { include '*.js' }
    }
    into OUTPUT_DIR_JS_LIB
}

task copyProjectDependenciesJs(type: Copy) {
    configurations.projectDependenciesJs.each {
        def jarJs = it.absolutePath.replace("-metadata-", "-js-")
        println "project dependency: $jarJs"
        from zipTree(jarJs).matching { include '*.js' }
    }
    into OUTPUT_DIR_JS_LIB
}

task copyMainJs(type: Copy) {
    from "${project.buildDir}/classes/kotlin/js/main"
    include "*.js"
    into OUTPUT_DIR_JS
}

build.dependsOn += copyLibraryDependenciesJs
build.dependsOn += copyProjectDependenciesJs
build.dependsOn += copyMainJs

//println "======================================="
//build.dependsOn.forEach {
//    println it
//}
