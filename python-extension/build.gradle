plugins {
    id 'kotlin-multiplatform'
    id 'org.jetbrains.gradle.plugin.idea-ext'
}


import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

def cfg = project.buildSettings

task testBuildSettingsYml() {
    doLast {
        println cfg.python_bin_path
        println cfg.python_include_path
    }
}

def currentOs = DefaultNativePlatform.getCurrentOperatingSystem()

kotlin {
    if (cfg.python_include_path != null) {
        if (currentOs.isMacOsX()) {
            macosX64() {
                binaries {
                    staticLib {
                        baseName = "datalore-plot-${project.name}"
                    }
                }

                compilations.main.cinterops {
                    python {
                        compilerOpts "-I${cfg.python_include_path}"
                    }
                }
            }
        }

        if (currentOs.isLinux()) {
            linuxX64() {
                binaries {
                    sharedLib {
                        baseName = "datalore-plot-${project.name}"
                    }
                }

                compilations.main.cinterops {
                    python {
                        compilerOpts "-I${cfg.python_include_path}"
                    }
                }
            }
        }
    } else {
        throw new GradleException('''
------------------------------------------------------------------------------------------------------
Python package build unavailable. Please set "python_include_path" property in build_settings.yml file.
------------------------------------------------------------------------------------------------------
''')
    }


    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')

                // (!) only `portable` sources
                implementation project(':base-portable')
                implementation project(':plot-config-portable')
            }
        }
    }
}